/* city_hall.pnml */

/*
    License: GPL v2
    https://github.com/jrook1445/wannaroo-extra-industries
*/

//@TODO:
// RFE: building gfx depend on town type/population

spritelayout cityhall_tile_1_layout {
	ground { sprite: (climate == CLIMATE_TEMPERATE) ? 2184 : 2182; }
	building { sprite: (climate == CLIMATE_TEMPERATE) ? 2186 : 2180; }
}

spritelayout cityhall_tile_2_layout {
	ground { sprite: (climate == CLIMATE_TEMPERATE) ? 2185 : 2183; }
	building { sprite: (climate == CLIMATE_TEMPERATE) ? 2187 : 2181; }
}

item(FEAT_INDUSTRYTILES, cityhall_tile_1, 15) {
	property {
		substitute: 0;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
	graphics {
		cityhall_tile_1_layout;
	}
}

item(FEAT_INDUSTRYTILES, cityhall_tile_2, 16) {
	property {
		substitute: 0;
		special_flags: bitmask(INDTILE_FLAG_ACCEPT_ALL);
	}
	graphics {
		cityhall_tile_2_layout;
	}
}

tilelayout cityhall_layout_1 {
	0, 0: cityhall_tile_1;
	1, 0: cityhall_tile_2;
}

produce(industry_city_hall_produce_256_pass, [], [PASS: LOAD_PERM(0x02)+extra_callback_info1%4;], 0)
produce(industry_city_hall_produce_256_tour, [], [TOUR: LOAD_PERM(0x02)+extra_callback_info1%4;], 0)

produce(industry_city_hall_produce_cargo_arrival_pass, [PASS: incoming_cargo_waiting("PASS");], [PASS: incoming_cargo_waiting("PASS")/4;], [STORE_PERM(1, 0x0F), STORE_PERM(0, 0x00), 0])
produce(industry_city_hall_produce_cargo_arrival_tour, [TOUR: incoming_cargo_waiting("TOUR");], [TOUR: incoming_cargo_waiting("TOUR")/4;], [STORE_PERM(1, 0x0F), STORE_PERM(0, 0x00), 0])

//produce(industry_city_hall_produce_cargo_arrival, waiting_cargo_1, waiting_cargo_2, waiting_cargo_3, (waiting_cargo_1/2 + waiting_cargo_2/4 + waiting_cargo_3/4)*param_enable_tour_prod, 0, 0);

switch (FEAT_INDUSTRIES, SELF, industry_city_hall_produce_256, cargotype_available("TOUR")) {
    1: industry_city_hall_produce_256_tour;
    industry_city_hall_produce_256_pass;
}

switch (FEAT_INDUSTRIES, SELF, industry_city_hall_produce_cargo_arrival, cargotype_available("TOUR")) {
    1: industry_city_hall_produce_cargo_arrival_tour;
    industry_city_hall_produce_cargo_arrival_pass;
}

switch (FEAT_INDUSTRIES, SELF, industry_city_hall_info_not_a_city, [LOAD_PERM(0x05), STORE_TEMP(LOAD_PERM(0x06), 256), STORE_TEMP(LOAD_PERM(0x04), 257), STORE_TEMP(LOAD_PERM(0x05), 258)]) {
	0..2: return string(STR_INDUSTRY_CITY_HALL_INFO_TEXT_IND_TOWN);
	3: return string(STR_INDUSTRY_CITY_HALL_INFO_TEXT_PLANNED_TOWN);
	return string(STR_INDUSTRY_CITY_HALL_INFO_TEXT_VILLAGE);
}

switch (FEAT_INDUSTRIES, SELF, industry_city_hall_info_city, [LOAD_PERM(0x05), STORE_TEMP(LOAD_PERM(0x06), 256), STORE_TEMP(LOAD_PERM(0x04), 257), STORE_TEMP(LOAD_PERM(0x05), 258)]) {
	0: return string(STR_INDUSTRY_CITY_HALL_INFO_TEXT_METRO);
	1: return string(STR_INDUSTRY_CITY_HALL_INFO_TEXT_IND_CITY);
	return string(STR_INDUSTRY_CITY_HALL_INFO_TEXT_SMALL_CITY);
}

switch (FEAT_INDUSTRIES, SELF, industry_city_hall_info_text_2, LOAD_PERM(0x04)) {
	1: industry_city_hall_info_city;
	industry_city_hall_info_not_a_city;
}

switch (FEAT_INDUSTRIES, SELF, industry_city_hall_info_text, ( grf_future_status("\44\44\24\11") || grf_future_status("\6A\64\72\52")) ) {
	1: industry_city_hall_info_text_2;
	return CB_RESULT_NO_TEXT;
}

switch (FEAT_INDUSTRIES, SELF, get_town_info_2, [STORE_PERM(LOAD_TEMP(0), 0x04), STORE_PERM( (LOAD_PERM(0x04)==1 ? getbits(LOAD_TEMP(1),0,2) : getbits(LOAD_TEMP(1),0,3)), 0x05), STORE_PERM(LOAD_TEMP(2), 0x06)]) {
	industry_monthly_prod_change_switch; //return CB_RESULT_IND_PROD_NO_CHANGE;
}

switch (FEAT_INDUSTRIES, PARENT, get_town_info, [STORE_TEMP(var[0x40], 0), STORE_TEMP(var[0x41], 1), STORE_TEMP(population, 2)]) {
	get_town_info_2;
}

switch (FEAT_INDUSTRIES, SELF, town_square_distance_check, industry_distance(0x02, str2number("jdr\02"))) {
	0..11: return string(STR_TOUR_INDUSTRY_TOO_CLOSE);
	return CB_RESULT_LOCATION_ALLOW;
}

switch (FEAT_INDUSTRIES, SELF, tower_distance_check, industry_distance(0x01, str2number("jdr\02"))) {
	0..11: return string(STR_TOUR_INDUSTRY_TOO_CLOSE);
	town_square_distance_check;
}

switch (FEAT_INDUSTRIES, SELF, hotel_distance_check, industry_distance(0x00, str2number("jdr\02"))) {
	0..11: return string(STR_TOUR_INDUSTRY_TOO_CLOSE);
	tower_distance_check;
}

switch (FEAT_INDUSTRIES, SELF, industry_city_hall_monopoly_check, industry_town_count(industry_city_hall, -1)) {
	0: hotel_distance_check;
	return string(STR_ONE_INDUSTRY_PER_TOWN);
}

if (param_enable_city_hall) {

item(FEAT_INDUSTRIES, industry_city_hall, IND_CITY_HALL) {
	property {
		substitute:			            INDUSTRYTYPE_POWER_PLANT;
		layouts:                        [cityhall_layout_1];
		life_type:                      IND_LIFE_TYPE_BLACK_HOLE;
		fund_cost_multiplier:           101;
		remove_cost_multiplier:		    0;
		cargo_types:                    [accept_cargo("PASS"), produce_cargo("PASS", 0)];
		min_cargo_distr:                5;
		conflicting_ind_types:          [];
		spec_flags:                     bitmask(IND_FLAG_ONLY_IN_TOWNS, IND_FLAG_RANDOM_BITS_IN_PRODUCTION_CALLBACK);
		prob_map_gen:			        0;
		prob_in_game:			        0;
		prospect_chance:                1.0;
		map_colour:                     103;
		name:                           string(STR_CITY_HALL_NAME);
		nearby_station_name:            string(STR_CITY_HALL_STATION_NAME);
	}

	if (cargotype_available("TOUR")) {
	    property {
	        cargo_types:                [accept_cargo("TOUR"), produce_cargo("TOUR", 0)];
        }
    }

	graphics {
		location_check:			        industry_city_hall_monopoly_check;
		produce_256_ticks:		        industry_city_hall_produce_256;
		produce_cargo_arrival:		    industry_city_hall_produce_cargo_arrival;
		build_prod_change:		        [STORE_PERM(9 + extra_callback_info2%4, 0x02), 16];
		monthly_prod_change:		    get_town_info; //return CB_RESULT_IND_PROD_NO_CHANGE;
		random_prod_change:		        industry_random_prod_change_switch; //return CB_RESULT_IND_PROD_NO_CHANGE;
		cargo_subtype_display:		    industry_remove_cargo_text;
		extra_text_industry:		    industry_city_hall_info_text;
	}
}

}

