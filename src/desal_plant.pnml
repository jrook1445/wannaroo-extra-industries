/* desal_plant.pnml */

/*
    License: GPL v2
    https://github.com/jrook1445/wannaroo-extra-industries
*/

tilelayout desal_plant_layout_se {
	0, 0: jrdesalplant_tile_talltanks_se;
	0, 1: jrdesalplant_tile_widetank;
	1, 1: jrdesalplant_tile_pump;
	1, 0: jrdesalplant_tile_pump_sloped;
}

tilelayout desal_plant_layout_sw {
	0, 0: jrdesalplant_tile_talltanks_sw;
	0, 1: jrdesalplant_tile_pump_sloped;
	1, 1: jrdesalplant_tile_pump;
	1, 0: jrdesalplant_tile_widetank;
}

tilelayout desal_plant_layout_nw {
	0, 0: jrdesalplant_tile_widetank;
	0, 1: jrdesalplant_tile_talltanks_nw;
	1, 1: jrdesalplant_tile_pump_sloped;
	1, 0: jrdesalplant_tile_pump;
}

tilelayout desal_plant_layout_ne {
	0, 0: jrdesalplant_tile_widetank;
	0, 1: jrdesalplant_tile_pump;
	1, 1: jrdesalplant_tile_pump_sloped;
	1, 0: jrdesalplant_tile_talltanks_ne;
}

INDUSTRY_LOCATION_SWITCHES(IND_DESAL_PLANT)

produce(industry_desal_plant_produce_256, 0, 0, 0, LOAD_PERM(0x02)+extra_callback_info1%3, LOAD_PERM(0x03)+extra_callback_info1%3, 0);

if (param_enable_desal_plant && (cargotype_available("WATR") || cargotype_available("RFPR"))) {

item(FEAT_INDUSTRIES, industry_desal_plant, IND_DESAL_PLANT) {
	property {
		substitute:			            INDUSTRYTYPE_POWER_PLANT;
		layouts:                        [desal_plant_layout_se, desal_plant_layout_sw, desal_plant_layout_nw, desal_plant_layout_ne];
		life_type:                      IND_LIFE_TYPE_PROCESSING;
		fund_cost_multiplier:           200; // same as petrol plant
		remove_cost_multiplier:		    0;
		accept_cargo_types:             [];
		//prod_cargo_types:               [( (climate == CLIMATE_TROPIC) ? cargotype("WATR") : cargotype("RFPR") ), ( (climate == CLIMATE_TROPIC) ? cargotype("RFPR") : cargotype_available("RCYC") ? cargotype("RCYC") : cargotype("GRVL") )];
		prod_cargo_types:               [cargotype("WATR"), cargotype_available("RFPR") ? cargotype("RFPR") : cargotype_available("ENSP") ? cargotype("ENSP") : cargotype("GOOD")];
		prod_multiplier:                [0, 0];
		input_multiplier_1:             [0, 0];
		input_multiplier_2:             [0, 0];
		input_multiplier_3:             [0, 0];
		min_cargo_distr:                5;
		conflicting_ind_types:          [];
		spec_flags:                     bitmask(IND_FLAG_RANDOM_BITS_IN_PRODUCTION_CALLBACK);
		prob_map_gen:			        param_manual_placement ? 0 : 2;
		prob_in_game:			        param_manual_placement ? 0 : 2;
		prospect_chance:                1.0;
		map_colour:                     151;
		name:                           string(STR_DESAL_PLANT_NAME);
		nearby_station_name:            string(STR_DESAL_PLANT_STATION_NAME);
	}
    if (!cargotype_available("WATR")) { property { prod_cargo_types: [cargotype("RFPR"), cargotype_available("GRVL") ? cargotype("GRVL") : cargotype_available("FMSP") ? cargotype("FMSP") : cargotype("GOOD")]; } }
/*
	// if not WRBI, then produce alt cargos
	if (!grf_future_status("\6A\64\72\83")) {
        // FIRS 1-3
        if (grf_future_status("\F1\25\00\00", "\FF\FF\FF\00")) { property { prod_cargo_types: [cargotype_available("RFPR") ? cargotype("RFPR") : cargotype_available("KAOL") ? cargotype("KAOL") : cargotype_available("RCYC") ? cargotype("RCYC") : cargotype("PASS"), cargotype("FMSP")]; } }
        // Manpower Industries, OpenGFX+ Industries, others
        else { property { prod_cargo_types: [cargotype_available("WATR") ? cargotype("WATR") : cargotype_available("RCYC") ? cargotype("RCYC") : cargotype_available("TOUR") ? cargotype("TOUR") : cargotype("PASS"), cargotype("GOOD")]; } }
    }
*/
	graphics {
		location_check:			        check_nearby_IND_DESAL_PLANT_industry;
		produce_256_ticks:		        industry_desal_plant_produce_256;
		build_prod_change:		        [STORE_PERM(12+extra_callback_info2%5, 0x02), STORE_PERM((extra_callback_info2>=0?1:-1) * getbits(extra_callback_info2,3,23)%3 + 8, 0x03), 16];
		monthly_prod_change:		    industry_primary_check_transported;
		random_prod_change:		        industry_random_prod_change_switch;
		colour:			                (founder == 16) ? COLOUR_LIGHT_BLUE : founder_colour1;
		//cargo_subtype_display:		    industry_remove_cargo_text;
	}
}

}

